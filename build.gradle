plugins {
    id 'base'
    id 'maven-publish'
    id 'fabric-loom' version '1.13-SNAPSHOT' apply false
    id 'com.replaymod.preprocess' version 'd452ef7612'
}

preprocess {
    strictExtraMappings = true

    def mc12001 = createNode('1.20.1', 1_20_01, 'mojang')
    def mc12004 = createNode('1.20.4', 1_20_04, 'mojang')
    def mc12101 = createNode('1.21.1', 1_21_01, 'mojang')
    def mc12104 = createNode('1.21.4', 1_21_04, 'mojang')
    def mc12110 = createNode('1.21.10', 1_21_10, 'mojang')

    mc12004.link(mc12001, null)
    mc12101.link(mc12004, null)
    mc12104.link(mc12101, null)
    mc12104.link(mc12110, null)
}

tasks.register('buildAndGather') {
    group = 'build'
    description = 'Builds all versions and gathers them into root build/libs'

    subprojects {
        if (project.name != 'fabricWrapper') {
            dependsOn project.tasks.named('build').get()
        }
    }

    doLast {
        def rootBuildLibs = rootProject.layout.buildDirectory.dir('libs/versions').get().asFile
        rootBuildLibs.mkdirs()

        subprojects {
            if (project.name == 'fabricWrapper') {
                return
            }

            def mcVersion = project.name
            def subprojectLibs = project.layout.buildDirectory.dir('libs').get().asFile

            def minecraftDep = project.findProperty("minecraft_dependency")?.toString() ?: mcVersion
            def versionSuffix = convertDependencyToSuffix(minecraftDep)

            copy {
                from(subprojectLibs) {
                    include '*.jar'
                    exclude '*-dev.jar', '*-sources.jar', '*-shadow.jar'
                }
                into rootBuildLibs

                rename { filename ->
                    def baseName = filename.replaceAll(/\.jar$/, '')
                    if (!baseName.contains("-mc")) {
                        return "${baseName}-mc${versionSuffix}.jar"
                    }
                    return filename
                }

                duplicatesStrategy = DuplicatesStrategy.INCLUDE
            }
        }
    }
}

static def convertDependencyToSuffix(String dependency) {
    def dep = dependency.replaceAll(/\s+/, '')

    def rangePattern = ~/>=([0-9.]+)<=([0-9.]+)/
    def rangeMatcher = rangePattern.matcher(dep)
    if (rangeMatcher.find()) {
        def minVersion = rangeMatcher.group(1)
        def maxVersion = rangeMatcher.group(2)
        return "${minVersion}-${maxVersion}"
    }

    def minPattern = ~/>=([0-9.]+)/
    def minMatcher = minPattern.matcher(dep)
    if (minMatcher.find()) {
        def version = minMatcher.group(1)
        return "${version}+"
    }

    def tildePattern = ~/~([0-9.]+)/
    def tildeMatcher = tildePattern.matcher(dep)
    if (tildeMatcher.find()) {
        def version = tildeMatcher.group(1)
        return "${version}"
    }

    return dependency
}