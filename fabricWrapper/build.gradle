import groovy.json.JsonBuilder
import groovy.json.JsonSlurper

plugins {
    id("java-library")
    id("maven-publish")
}

group = project.maven_group
version = project.mod_version

base {
    archivesName.set("${project.archives_base_name}-versionpack")
}

def fabric_subprojects = project.parent.subprojects.findAll {
    it.name != "fabricWrapper"
}

fabric_subprojects.collect {
    evaluationDependsOn(":${it.name}")
}

tasks.register('collectSubModules') {
    outputs.upToDateWhen { false }

    dependsOn(rootProject.tasks.named('buildAndGather'))

    doFirst {
        delete fileTree("build/tmp/submods/META-INF/jars")

        copy {
            from(rootProject.layout.buildDirectory.dir('libs/versions').get().asFile) {
                include '*.jar'
                exclude '*-dev.jar', '*-sources.jar', '*-shadow.jar'
            }
            into("build/tmp/submods/META-INF/jars")
            duplicatesStrategy = DuplicatesStrategy.INCLUDE
        }
    }
}

jar {
    outputs.upToDateWhen { false }
    dependsOn collectSubModules
    from("${rootDir}/LICENSE")
    from("build/tmp/submods")
    destinationDirectory.set(rootProject.layout.buildDirectory.dir('libs').get().asFile)
}

processResources {
    outputs.upToDateWhen { false }

    dependsOn collectSubModules

    from("${rootDir}/icon.png") {
        into("assets/${project.mod_id}")
    }

    filesMatching("fabric.mod.json") {
        expand([
                "description": project.mod_description,
                "homepage": project.mod_homepage,
                "id": project.mod_id,
                "license": project.mod_license,
                "name": project.mod_name,
                "version": project.mod_version,
                "sources": project.mod_sources
        ])
    }

    doLast {
        ArrayList<?> mc_condition = []
        ArrayList<?> jars = []

        def jarToProjectMap = [:]
        fabric_subprojects.each { subproject ->
            def minecraftDep = subproject.findProperty("minecraft_dependency")?.toString() ?: subproject.name
            def versionSuffix = rootProject.convertDependencyToSuffix(minecraftDep)
            def expectedJarName = "${project.archives_base_name}-${project.mod_version}-mc${versionSuffix}.jar"
            jarToProjectMap[expectedJarName] = subproject
        }

        def jarsDir = file("build/tmp/submods/META-INF/jars")
        if (jarsDir.exists()) {
            jarsDir.listFiles().findAll { it.name.endsWith('.jar') }.each { jarFile ->
                def matchingProject = jarToProjectMap[jarFile.name]

                if (matchingProject) {
                    mc_condition.add("${matchingProject.minecraft_dependency}")
                }

                jars.add(["file": "META-INF/jars/${jarFile.name}"])
            }
        }

        File file = file("build/resources/main/fabric.mod.json")
        JsonSlurper slurper = new JsonSlurper()
        JsonBuilder builder = new JsonBuilder(slurper.parse(file))
        builder.content.depends.minecraft = mc_condition
        builder.content.jars = jars
        BufferedWriter writer = file.newWriter()
        writer.append(builder.toPrettyString())
        writer.flush()
        writer.close()
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

publishing {
    publications {
        register("mavenJava", MavenPublication) {
            groupId = "${project.maven_group}"
            artifactId = "${project.mod_id}"
            version = "versionpack-" + version
            from components.java
        }
    }

    repositories {
        mavenLocal()
        maven {
            url = "$rootDir/publish"
        }
    }
}