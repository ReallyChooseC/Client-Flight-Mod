apply plugin: 'maven-publish'
apply plugin: 'fabric-loom'
apply plugin: 'com.replaymod.preprocess'

int mcVersion = project.mcVersion

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

JavaVersion JAVA_COMPATIBILITY
if (mcVersion >= 12005) {
    JAVA_COMPATIBILITY = JavaVersion.VERSION_21
} else if (mcVersion >= 11800) {
    JAVA_COMPATIBILITY = JavaVersion.VERSION_17
} else {
    JAVA_COMPATIBILITY = JavaVersion.VERSION_1_8
}

loom {
    runConfigs.configureEach {
        ideConfigGenerated = true
        runDir '../../run'
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        def fabric_api
        if (mcVersion > 12001) {
            fabric_api = "fabric-api"
        } else {
            fabric_api = "fabric"
        }

        expand(
                "id": project.mod_id,
                "name": project.mod_name,
                "version": inputs.properties.version,
                "fabric_api": fabric_api,
                "minecraft_dependency": project.minecraft_dependency,
        )
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = JAVA_COMPATIBILITY.ordinal() + 1
}

java {
    sourceCompatibility = JAVA_COMPATIBILITY
    targetCompatibility = JAVA_COMPATIBILITY
    withSourcesJar()
}

jar {
    from(rootProject.file('LICENSE')) {
        rename { "${it}_${project.archives_base_name}" }
    }
}

publishing {
    publications {
        create('mavenJava', MavenPublication) {
            artifactId = base.archivesName.get()
            from components.java
        }
    }

    repositories {
    }
}